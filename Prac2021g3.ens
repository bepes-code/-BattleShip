TECLAT EQU 0A000h
PANTALLA EQU 0B000h

ORIGEN 600h
INICIO ini
.DATOS
	eps VALOR 0C45h, 1370h, 1553h ;"E","p" i "S" com entrarà per teclat
	codi VALOR 3843h, 384Fh, 3844h, 3849h; "C","O", "D" i "I" amb fons blanc i lletra negra 
	ok VALOR 1046h,1045h,104Ch,1049h,1043h,1049h,1054h,1041h,1054h,1053h,0733h,0730h,0720h,0741h,074Eh,0759h,0753h; FELICITATS3O ANYS amb el format adequat
	er VALOR 2045h, 2052h, 2052h, 204Fh,2052h; ERROR amb fons vermell i lletra negra (20h) 
	usuari VALOR 3 VECES 0000h
.CODIGO
ini:
	MOVL R0, BYTEBAJO TECLAT
	MOVH R0, BYTEALTO TECLAT; R0=A000h

	MOVL R1, BYTEBAJO PANTALLA
	MOVH R1, BYTEALTO PANTALLA; R1=B000h

;Esborrar tots els caràcters del buffer de teclat
	MOV R2,R0
	INC R2; R2=A001h CONTROL DEL TECLAT
	
	MOVH R3,00h
	MOVL R3,00000100b; R3=0004h ordre de esborrar tots els caràcters del teclat

	MOV [R2],R3
	
;Esborrar pantalla
	MOV R3,R1; R3=B000h
	MOVL R3,78h; R3=B078h=B000h+120d

	MOVH R4,00h
	MOVL R4,00000011b; R4=03h ordre d'engegar i esborrar lo que hi hagi a la pantalla

	MOV [R3],R4

;RO TECLAT, R1 PANTALLA, R2 CONTROL TECLAT, R3 CONTROL PANTALLA, R4 "LLIURE" , R5,R6 I R7 LLIURES
	MOVL R3,2Dh; B000h+45d=B02Dh R3 PUNTER PANTALLA
	MOVL R4,04h; R4 ITERADOR BUCLE ESCRIURE CODI
	MOVH R4,00h; 
	MOVH R5, BYTEALTO DIRECCION codi
	MOVL R5, BYTEBAJO DIRECCION codi; R5 és l'adreça on es troba "C"
	
	es_codi:
		MOV R6,[R5]
		MOV [R3],R6
		INC R5; SEGUENT CARACTER
		INC R3; SEGUENT POSICIO
		DEC R4
		BRNZ es_codi

;RO TECLAT, R1 usuari, R2 CONTROL TECLAT, R3 POSICIÓ PANTALLA, R4 0003 ITERADOR PER AGAFAR EpS , R5 CONTINGUT BUFFER TECLAT,R6 HI HA TECLA? I R7 LLIURES
	INC R3 
	MOVL R4,03h; NECESSITO 3 CARACTERS	

	MOVL R1, BYTEBAJO DIRECCION usuari
	MOVH R1, BYTEALTO DIRECCION usuari; 
		

;Esperar tecla al buffer de teclat
	MOVL R6,00h
	MOVH R6,01h
		
	tres_lletres:
		no_tecla:
			MOV R5,[R2]
			MOVL R5,00h; 	
			COMP R5,R6; SI R5 VAL 0100 HI HA TECLA, SI VAL 0000 NO HI HA TECLA
				  ;si COMP R5,R6= 0 vol dir que R5=R6, else R5!=R6 
			BRNZ no_tecla
		;tinc tecla al buffer-> emmagatzemar-la i imprimir-la per pantalla
		MOV R5,[R0]
		MOV [R1],R5
		MOVH R5,18h; al R5 hi ha la tecla que s'ha pitjat, pero abans de treure-la per pantalla li poso el format que vull (fons groc lletra negra= 18h)
		MOV [R3],R5;IMPRIMIR PER PANTALLA
		
		INC R1
		INC R3
		DEC R4
		BRNZ tres_lletres
	
;R3=POSICIO PANTALLA, R1 USUARI, R0,R2,R4=0000h,R5,R6 I R7 LLIURES	

;COMPARAR EL MISSATGE EMMAGATZEMAT I EL MISSATGE DE L'USUARI
	MOVL R1, BYTEBAJO DIRECCION usuari

	MOVL R0, BYTEBAJO DIRECCION eps
	MOVH R0, BYTEALTO DIRECCION eps
	
	MOVL R4,03
	com_eps:
		MOV R2,[R1];R2 LLETRA PITJADA PER L'USUARI
		MOV R5,[R0];R5 LLETRA CORRESPONENT A EpS
		COMP R2,R5
		BRNZ incorrecte
		INC R1
		INC R0
		DEC R4
		BRNZ com_eps

	;l'usuari ha posat EpS i R4=0000H
	MOVL R3, 4Dh; per escriure felicitats
	MOVL R4,0Ah; iterador de 10 posicions (felicitats té 10 lletres)
	MOVL R0, BYTEBAJO DIRECCION ok
	MOVH R0, BYTEALTO DIRECCION ok
	es_fel:
		MOV R1,[R0]
		MOV [R3],R1
		INC R0
		INC R3
		DEC R4
		BRNZ es_fel
;R4= 0000h
	MOVL R3, 5Dh; per escriure 30 ANYS
	MOVL R4,07h; iterador de 7 posicions (30 ANYS té 7 lletres)
	es_trenta:
		MOV R1,[R0]
		MOV [R3],R1
		INC R0
		INC R3
		DEC R4
		BRNZ es_trenta
	JMP fi
	incorrecte: ;imprimir ERROR
	MOVL R3, 73h; per escriure error
	MOVL R4,05h; iterador de 5 posicions (error té 5 lletres)
	MOVL R0, BYTEBAJO DIRECCION er
	MOVH R0, BYTEALTO DIRECCION er
	es_er:
		MOV R1,[R0]
		MOV [R3],R1
		INC R0
		INC R3
		DEC R4
		BRNZ es_er

	fi:
		JMP -1
FIN
	

	

